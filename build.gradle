plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.2'
	id 'io.spring.dependency-management' version '1.1.6'
	id 'jacoco'
}

group = 'br.com.mglu'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

jacoco {
	toolVersion = "0.8.12"
	reportsDirectory = layout.buildDirectory.dir('reports/jacoco')
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'com.fasterxml.jackson.core:jackson-annotations:2.13.1'

	implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
	implementation 'org.postgresql:r2dbc-postgresql:1.0.4.RELEASE'
	implementation 'io.r2dbc:r2dbc-pool'

	implementation(platform("software.amazon.awssdk:bom:2.25.61"))
	implementation("software.amazon.awssdk:s3")

	// Lombok
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test'
	testImplementation 'org.jeasy:easy-random-core:5.0.0'

	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
}

test {
	finalizedBy jacocoTestReport
}

jacocoTestReport {
	dependsOn test
}

jacocoTestReport {
	reports {
		xml.required = false
		csv.required = false
		html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
	}

	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude: ['**/*Controller.*',
										'**/*Filter.*',
										'**/*Response.*',
										'**/*Repository.*',
										'**/*OrderapiApplication**',
										'**/orderapi/exception/*',
										'**/orderapi/config/**',
										'**/orderapi/aws/**'])
		}))
	}

}

jacocoTestCoverageVerification {

	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude: ['**/*Controller.*',
										'**/*Filter.*',
										'**/*Response.*',
										'**/*Repository.*',
										'**/*OrderapiApplication**',
										'**/orderapi/exception/*',
										'**/orderapi/config/**',
										'**/orderapi/aws/**'])
		}))
	}

	violationRules {
		rule {
			limit {
				minimum = 0.8
			}
		}
	}
}
check.dependsOn jacocoTestCoverageVerification
